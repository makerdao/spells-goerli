// SPDX-License-Identifier: AGPL-3.0-or-later
//
// Copyright (C) 2021-2022 Dai Foundation
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

pragma solidity 0.6.12;

import "./Goerli-DssSpell.t.base.sol";

interface DenyProxyLike {
    function denyProxy(address) external;
}

contract DssSpellTest is GoerliDssSpellTestBase {

    function test_OSM_auth() private {  // make public to use
        // address ORACLE_WALLET01 = 0x4D6fbF888c374D7964D56144dE0C0cFBd49750D3;

        // validate the spell does what we told it to
        //bytes32[] memory ilks = reg.list();

        //for(uint256 i = 0; i < ilks.length; i++) {
        //    uint256 class = reg.class(ilks[i]);
        //    if (class != 1) { continue; }

        //    address pip = reg.pip(ilks[i]);
        //    // skip USDC, TUSD, PAXUSD, GUSD
        //    if (pip == 0x838212865E2c2f4F7226fCc0A3EFc3EB139eC661 ||
        //        pip == 0x0ce19eA2C568890e63083652f205554C927a0caa ||
        //        pip == 0xdF8474337c9D3f66C0b71d31C7D3596E4F517457 ||
        //        pip == 0x57A00620Ba1f5f81F20565ce72df4Ad695B389d7) {
        //        continue;
        //    }

        //    assertEq(OsmAbstract(pip).wards(ORACLE_WALLET01), 0);
        //}

        //vote(address(spell));
        //scheduleWaitAndCast(address(spell));
        //assertTrue(spell.done());

        //for(uint256 i = 0; i < ilks.length; i++) {
        //    uint256 class = reg.class(ilks[i]);
        //    if (class != 1) { continue; }

        //    address pip = reg.pip(ilks[i]);
        //    // skip USDC, TUSD, PAXUSD, GUSD
        //    if (pip == 0x838212865E2c2f4F7226fCc0A3EFc3EB139eC661 ||
        //        pip == 0x0ce19eA2C568890e63083652f205554C927a0caa ||
        //        pip == 0xdF8474337c9D3f66C0b71d31C7D3596E4F517457 ||
        //        pip == 0x57A00620Ba1f5f81F20565ce72df4Ad695B389d7) {
        //        continue;
        //    }

        //    assertEq(OsmAbstract(pip).wards(ORACLE_WALLET01), 1);
        //}
    }

    function test_oracle_list() private {  // make public to use
        // address ORACLE_WALLET01 = 0x4D6fbF888c374D7964D56144dE0C0cFBd49750D3;

        //assertEq(OsmAbstract(0xF15993A5C5BE496b8e1c9657Fd2233b579Cd3Bc6).wards(ORACLE_WALLET01), 0);

        //vote(address(spell));
        //scheduleWaitAndCast(address(spell));
        //assertTrue(spell.done());

        //assertEq(OsmAbstract(0xF15993A5C5BE496b8e1c9657Fd2233b579Cd3Bc6).wards(ORACLE_WALLET01), 1);
    }

    function testSpellIsCast_GENERAL() public {
        string memory description = new DssSpell().description();
        assertTrue(bytes(description).length > 0, "TestError/spell-description-length");
        // DS-Test can't handle strings directly, so cast to a bytes32.
        assertEq(stringToBytes32(spell.description()),
                stringToBytes32(description), "TestError/spell-description");

        if(address(spell) != address(spellValues.deployed_spell)) {
            assertEq(spell.expiration(), block.timestamp + spellValues.expiration_threshold, "TestError/spell-expiration");
        } else {
            assertEq(spell.expiration(), spellValues.deployed_spell_created + spellValues.expiration_threshold, "TestError/spell-expiration");

            // If the spell is deployed compare the on-chain bytecode size with the generated bytecode size.
            // extcodehash doesn't match, potentially because it's address-specific, avenue for further research.
            address depl_spell = spellValues.deployed_spell;
            address code_spell = address(new DssSpell());
            assertEq(getExtcodesize(depl_spell), getExtcodesize(code_spell), "TestError/spell-codesize");
        }

        assertTrue(spell.officeHours() == spellValues.office_hours_enabled, "TestError/spell-office-hours");

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done(), "TestError/spell-not-done");

        checkSystemValues(afterSpell);

        checkCollateralValues(afterSpell);
    }

    function testRemoveChainlogValues() private { // make public to use
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // try chainLog.getAddress("XXX") {
        //     assertTrue(false);
        // } catch Error(string memory errmsg) {
        //     assertTrue(cmpStr(errmsg, "dss-chain-log/invalid-key"));
        // } catch {
        //     assertTrue(false);
        // }

        try chainLog.getAddress("VAL_ETH") {
            assertTrue(false);
        } catch Error(string memory errmsg) {
            assertTrue(cmpStr(errmsg, "dss-chain-log/invalid-key"));
        } catch {
            assertTrue(false);
        }

        try chainLog.getAddress("VAL_BAT") {
            assertTrue(false);
        } catch Error(string memory errmsg) {
            assertTrue(cmpStr(errmsg, "dss-chain-log/invalid-key"));
        } catch {
            assertTrue(false);
        }

        try chainLog.getAddress("VAL_USDC") {
            assertTrue(false);
        } catch Error(string memory errmsg) {
            assertTrue(cmpStr(errmsg, "dss-chain-log/invalid-key"));
        } catch {
            assertTrue(false);
        }

        try chainLog.getAddress("DEPLOYER") {
            assertTrue(false);
        } catch Error(string memory errmsg) {
            assertTrue(cmpStr(errmsg, "dss-chain-log/invalid-key"));
        } catch {
            assertTrue(false);
        }
    }

    function testCollateralIntegrations() private {
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Insert new collateral tests here
        // checkIlkIntegration(
        //     "TOKEN-X",
        //     GemJoinAbstract(addr.addr("MCD_JOIN_TOKEN_X")),
        //     ClipAbstract(addr.addr("MCD_CLIP_TOKEN_X")),
        //     addr.addr("PIP_TOKEN"),
        //     true,
        //     true,
        //     false
        // );

        checkIlkIntegration(
             "WSTETH-B",
             GemJoinAbstract(addr.addr("MCD_JOIN_WSTETH_B")),
             ClipAbstract(addr.addr("MCD_CLIP_WSTETH_B")),
             addr.addr("PIP_WSTETH"),
             true,
             true,
             false
        );
    }

    function testLerpSurplusBuffer() private { // make public to use
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Insert new SB lerp tests here

        LerpAbstract lerp = LerpAbstract(lerpFactory.lerps("NAME"));

        uint256 duration = 210 days;
        hevm.warp(block.timestamp + duration / 2);
        assertEq(vow.hump(), 60 * MILLION * RAD);
        lerp.tick();
        assertEq(vow.hump(), 75 * MILLION * RAD);
        hevm.warp(block.timestamp + duration / 2);
        lerp.tick();
        assertEq(vow.hump(), 90 * MILLION * RAD);
        assertTrue(lerp.done());
    }

    function testNewChainlogValues() public { // make public to use
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Insert new chainlog values tests here
        assertEq(chainLog.getAddress("MCD_END"), addr.addr("MCD_END"));
        assertEq(chainLog.getAddress("MCD_CURE"), addr.addr("MCD_CURE"));
        assertEq(chainLog.getAddress("MCD_FLASH"), addr.addr("MCD_FLASH"));
        assertEq(chainLog.getAddress("MCD_FLASH_LEGACY"), addr.addr("MCD_FLASH_LEGACY"));
        assertEq(chainLog.version(), "1.13.0");
    }

    function testNewIlkRegistryValues() private { // make public to use
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Insert new ilk registry values tests here
        assertEq(reg.pos("WSTETH-B"), 49);
        assertEq(reg.join("WSTETH-B"), addr.addr("MCD_JOIN_WSTETH_B"));
        assertEq(reg.gem("WSTETH-B"), addr.addr("WSTETH"));
        assertEq(reg.dec("WSTETH-B"), GemAbstract(addr.addr("WSTETH")).decimals());
        assertEq(reg.class("WSTETH-B"), 1);
        assertEq(reg.pip("WSTETH-B"), addr.addr("PIP_WSTETH"));
        assertEq(reg.xlip("WSTETH-B"), addr.addr("MCD_CLIP_WSTETH_B"));
        assertEq(reg.name("WSTETH-B"), "Wrapped liquid staked Ether 2.0");
        assertEq(reg.symbol("WSTETH-B"), "wstETH");
    }

    function testFailWrongDay() public {
        require(spell.officeHours() == spellValues.office_hours_enabled);
        if (spell.officeHours()) {
            vote(address(spell));
            scheduleWaitAndCastFailDay();
        } else {
            revert("Office Hours Disabled");
        }
    }

    function testFailTooEarly() public {
        require(spell.officeHours() == spellValues.office_hours_enabled);
        if (spell.officeHours()) {
            vote(address(spell));
            scheduleWaitAndCastFailEarly();
        } else {
            revert("Office Hours Disabled");
        }
    }

    function testFailTooLate() public {
        require(spell.officeHours() == spellValues.office_hours_enabled);
        if (spell.officeHours()) {
            vote(address(spell));
            scheduleWaitAndCastFailLate();
        } else {
            revert("Office Hours Disabled");
        }
    }

    function testOnTime() public {
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
    }

    function testCastCost() public {
        vote(address(spell));
        spell.schedule();

        castPreviousSpell();
        hevm.warp(spell.nextCastTime());
        uint256 startGas = gasleft();
        spell.cast();
        uint256 endGas = gasleft();
        uint256 totalGas = startGas - endGas;

        assertTrue(spell.done());
        // Fail if cast is too expensive
        assertTrue(totalGas <= 10 * MILLION);
    }

    // The specific date doesn't matter that much since function is checking for difference between warps
    function test_nextCastTime() public {
        hevm.warp(1606161600); // Nov 23, 20 UTC (could be cast Nov 26)

        vote(address(spell));
        spell.schedule();

        uint256 monday_1400_UTC = 1606744800; // Nov 30, 2020
        uint256 monday_2100_UTC = 1606770000; // Nov 30, 2020

        // Day tests
        hevm.warp(monday_1400_UTC);                                    // Monday,   14:00 UTC
        assertEq(spell.nextCastTime(), monday_1400_UTC);               // Monday,   14:00 UTC

        if (spell.officeHours()) {
            hevm.warp(monday_1400_UTC - 1 days);                       // Sunday,   14:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC);           // Monday,   14:00 UTC

            hevm.warp(monday_1400_UTC - 2 days);                       // Saturday, 14:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC);           // Monday,   14:00 UTC

            hevm.warp(monday_1400_UTC - 3 days);                       // Friday,   14:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC - 3 days);  // Able to cast

            hevm.warp(monday_2100_UTC);                                // Monday,   21:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC + 1 days);  // Tuesday,  14:00 UTC

            hevm.warp(monday_2100_UTC - 1 days);                       // Sunday,   21:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC);           // Monday,   14:00 UTC

            hevm.warp(monday_2100_UTC - 2 days);                       // Saturday, 21:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC);           // Monday,   14:00 UTC

            hevm.warp(monday_2100_UTC - 3 days);                       // Friday,   21:00 UTC
            assertEq(spell.nextCastTime(), monday_1400_UTC);           // Monday,   14:00 UTC

            // Time tests
            uint256 castTime;

            for(uint256 i = 0; i < 5; i++) {
                castTime = monday_1400_UTC + i * 1 days; // Next day at 14:00 UTC
                hevm.warp(castTime - 1 seconds); // 13:59:59 UTC
                assertEq(spell.nextCastTime(), castTime);

                hevm.warp(castTime + 7 hours + 1 seconds); // 21:00:01 UTC
                if (i < 4) {
                    assertEq(spell.nextCastTime(), monday_1400_UTC + (i + 1) * 1 days); // Next day at 14:00 UTC
                } else {
                    assertEq(spell.nextCastTime(), monday_1400_UTC + 7 days); // Next monday at 14:00 UTC (friday case)
                }
            }
        }
    }

    function testFail_notScheduled() public view {
        spell.nextCastTime();
    }

    function test_use_eta() public {
        hevm.warp(1606161600); // Nov 23, 20 UTC (could be cast Nov 26)

        vote(address(spell));
        spell.schedule();

        uint256 castTime = spell.nextCastTime();
        assertEq(castTime, spell.eta());
    }

    function test_Medianizers() private { // make public to use
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Track Median authorizations here
        address SET_TOKEN    = address(0);
        address TOKENUSD_MED = OsmAbstract(addr.addr("PIP_TOKEN")).src();
        assertEq(MedianAbstract(TOKENUSD_MED).bud(SET_TOKEN), 1);
    }

    function test_auth() public {
        checkAuth(false);
    }

    function test_auth_in_sources() public {
        checkAuth(true);
    }

    // Verifies that the bytecode of the action of the spell used for testing
    // matches what we'd expect.
    //
    // Not a complete replacement for Etherscan verification, unfortunately.
    // This is because the DssSpell bytecode is non-deterministic because it
    // deploys the action in its constructor and incorporates the action
    // address as an immutable variable--but the action address depends on the
    // address of the DssSpell which depends on the address+nonce of the
    // deploying address. If we had a way to simulate a contract creation by
    // an arbitrary address+nonce, we could verify the bytecode of the DssSpell
    // instead.
    //
    // Vacuous until the deployed_spell value is non-zero.
    function test_bytecode_matches() public {
        address expectedAction = (new DssSpell()).action();
        address actualAction   = spell.action();
        uint256 expectedBytecodeSize;
        uint256 actualBytecodeSize;
        assembly {
            expectedBytecodeSize := extcodesize(expectedAction)
            actualBytecodeSize   := extcodesize(actualAction)
        }

        uint256 metadataLength = getBytecodeMetadataLength(expectedAction);
        assertTrue(metadataLength <= expectedBytecodeSize);
        expectedBytecodeSize -= metadataLength;

        metadataLength = getBytecodeMetadataLength(actualAction);
        assertTrue(metadataLength <= actualBytecodeSize);
        actualBytecodeSize -= metadataLength;

        assertEq(actualBytecodeSize, expectedBytecodeSize);
        uint256 size = actualBytecodeSize;
        uint256 expectedHash;
        uint256 actualHash;
        assembly {
            let ptr := mload(0x40)

            extcodecopy(expectedAction, ptr, 0, size)
            expectedHash := keccak256(ptr, size)

            extcodecopy(actualAction, ptr, 0, size)
            actualHash := keccak256(ptr, size)
        }
        assertEq(expectedHash, actualHash);
    }

    // Validate addresses in test harness match chainlog
    function test_chainlog_values() public {
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        for(uint256 i = 0; i < chainLog.count(); i++) {
            (bytes32 _key, address _val) = chainLog.get(i);
            assertEq(_val, addr.addr(_key), concat("TestError/chainlog-addr-mismatch-", _key));
        }
    }

    // Ensure version is updated if chainlog changes
    function test_chainlog_version_bump() public {

        uint256                   _count = chainLog.count();
        string    memory        _version = chainLog.version();
        address[] memory _chainlog_addrs = new address[](_count);

        for(uint256 i = 0; i < _count; i++) {
            (, address _val) = chainLog.get(i);
            _chainlog_addrs[i] = _val;
        }

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        if (keccak256(abi.encodePacked(_version)) == keccak256(abi.encodePacked(chainLog.version()))) {
            // Fail if the version is not updated and the chainlog count has changed
            if (_count != chainLog.count()) {
                emit log_named_string("Error", concat("TestError/chainlog-version-not-updated-count-change-", _version));
                fail();
                return;
            }
            // Fail if the chainlog is the same size but local keys don't match the chainlog.
            for(uint256 i = 0; i < _count; i++) {
                (, address _val) = chainLog.get(i);
                if (_chainlog_addrs[i] != _val) {
                    emit log_named_string("Error", concat("TestError/chainlog-version-not-updated-address-change-", _version));
                    fail();
                    return;
                }
            }
        }
    }

    function tryVest(address vest, uint256 id) internal returns (bool ok) {
        (ok,) = vest.call(abi.encodeWithSignature("vest(uint256)", id));
    }

    function testVestDAI() private { // make public to use
        VestAbstract vest = VestAbstract(addr.addr("MCD_VEST_DAI"));

        assertEq(vest.ids(), 0);

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        assertEq(vest.ids(), 1);

        assertEq(vest.cap(), 1 * MILLION * WAD / 30 days);

        assertEq(vest.usr(1), address(pauseProxy));
        assertEq(vest.bgn(1), block.timestamp - 1 days);
        assertEq(vest.clf(1), block.timestamp - 1 days);
        assertEq(vest.fin(1), block.timestamp);
        assertEq(vest.mgr(1), address(0));
        assertEq(vest.res(1), 0);
        assertEq(vest.tot(1), WAD);
        assertEq(vest.rxd(1), 0);

        uint256 prevBalance = dai.balanceOf(address(pauseProxy));
        assertTrue(tryVest(address(vest), 1));
        assertEq(dai.balanceOf(address(pauseProxy)), prevBalance + WAD);

        assertEq(vest.rxd(1), WAD);
    }

    function testNewEndAuthorities() public {
        address endOld = chainLog.getAddress("MCD_END");
        assertEq(vat.wards(endOld), 1);
        assertEq(cat.wards(endOld), 1);
        assertEq(dog.wards(endOld), 1);
        assertEq(vow.wards(endOld), 1);
        assertEq(pot.wards(endOld), 1);
        assertEq(spotter.wards(endOld), 1);

        assertEq(EndAbstract(endOld).wards(address(esm)), 1);

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        // Contracts set
        assertEq(end.vat(), address(vat));
        assertEq(end.cat(), address(cat));
        assertEq(end.dog(), address(dog));
        assertEq(end.vow(), address(vow));
        assertEq(end.pot(), address(pot));
        assertEq(end.spot(), address(spotter));
        assertEq(EndNewAbstract(address(end)).cure(), address(cure));

        // Check end.wait
        assertEq(end.wait(), EndAbstract(endOld).wait());

        assertEq(esm.end(), address(end));

        // Check flippers/clippers authorities and osms whitelisting
        bytes32[] memory ilks = reg.list();
        for (uint256 i = 0; i < ilks.length; i++) {
            if (reg.class(ilks[i]) < 3) {
                FlipAbstract xlip = FlipAbstract(reg.xlip(ilks[i]));
                assertEq(xlip.wards(address(end)), 1);
                assertEq(xlip.wards(endOld), 0);

                OsmAbstract osm = OsmAbstract(reg.pip(ilks[i]));
                try osm.bud(address(123)) { // Check is an OSM or Median
                    assertEq(osm.bud(address(end)), 1);
                    assertEq(osm.bud(endOld), 0);
                } catch {}
            }
        }

        assertEq(vat.wards(endOld), 0);
        assertEq(cat.wards(endOld), 0);
        assertEq(dog.wards(endOld), 0);
        assertEq(vow.wards(endOld), 0);
        assertEq(pot.wards(endOld), 0);
        assertEq(spotter.wards(endOld), 0);
        assertEq(CureAbstract(cure).wards(endOld), 0);

        assertEq(EndAbstract(endOld).wards(address(esm)), 0);

        assertEq(vat.wards(address(end)), 1);
        assertEq(cat.wards(address(end)), 1);
        assertEq(dog.wards(address(end)), 1);
        assertEq(vow.wards(address(end)), 1);
        assertEq(pot.wards(address(end)), 1);
        assertEq(spotter.wards(address(end)), 1);
        assertEq(CureAbstract(cure).wards(address(end)), 1);

        assertEq(end.wards(address(esm)), 1);
    }

    function testNewEndFunctionality() public {
        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        GemAbstract ETH = GemAbstract(addr.addr("ETH"));
        GemJoinAbstract joinETHA = GemJoinAbstract(addr.addr("MCD_JOIN_ETH_A"));
        ClipAbstract clipETHA = ClipAbstract(addr.addr("MCD_CLIP_ETH_A"));

        GemAbstract YFI = GemAbstract(addr.addr("YFI"));
        GemJoinAbstract joinYFIA = GemJoinAbstract(addr.addr("MCD_JOIN_YFI_A"));
        ClipAbstract clipYFIA = ClipAbstract(addr.addr("MCD_CLIP_YFI_A"));

        GemAbstract LINK = GemAbstract(addr.addr("LINK"));
        GemJoinAbstract joinLINKA = GemJoinAbstract(addr.addr("MCD_JOIN_LINK_A"));
        ClipAbstract clipLINKA = ClipAbstract(addr.addr("MCD_CLIP_LINK_A"));

        uint256 ilkAmt = 10000 * WAD;

        giveTokens(address(ETH), ilkAmt);
        giveTokens(address(YFI), ilkAmt);
        giveTokens(address(LINK), ilkAmt);

        ETH.approve(address(joinETHA), ilkAmt);
        joinETHA.join(address(this), ilkAmt);
        YFI.approve(address(joinYFIA), ilkAmt);
        joinYFIA.join(address(this), ilkAmt);
        LINK.approve(address(joinLINKA), ilkAmt);
        joinLINKA.join(address(this), ilkAmt);

        // Don't worry about debt ceilings
        hevm.store(
            address(vat),
            bytes32(uint256(9)),
            bytes32(type(uint256).max)
        );
        hevm.store(
            address(vat),
            bytes32(uint256(keccak256(abi.encode(bytes32("ETH-A"), uint256(2)))) + 3),
            bytes32(type(uint256).max)
        );
        hevm.store(
            address(vat),
            bytes32(uint256(keccak256(abi.encode(bytes32("YFI-A"), uint256(2)))) + 3),
            bytes32(type(uint256).max)
        );
        hevm.store(
            address(vat),
            bytes32(uint256(keccak256(abi.encode(bytes32("LINK-A"), uint256(2)))) + 3),
            bytes32(type(uint256).max)
        );
        //

        (,uint256 rate, uint256 spot,,) = vat.ilks("ETH-A");
        vat.frob("ETH-A", address(this), address(this), address(this), int256(ilkAmt), int256(mul(ilkAmt, spot) / rate));
        (, rate, spot,,) = vat.ilks("YFI-A");
        vat.frob("YFI-A", address(this), address(this), address(this), int256(ilkAmt), int256(mul(ilkAmt, spot) / rate));
        (, rate, spot,,) = vat.ilks("LINK-A");
        vat.frob("LINK-A", address(this), address(this), address(this), int256(ilkAmt), int256(mul(ilkAmt, spot) / rate));

        hevm.warp(block.timestamp + 1);
        jug.drip("ETH-A");
        jug.drip("YFI-A");
        jug.drip("LINK-A");

        uint256 auctionIdETHA = clipETHA.kicks() + 1;
        uint256 auctionIdYFIA = clipYFIA.kicks() + 1;
        uint256 auctionIdLINKA = clipLINKA.kicks() + 1;

        dog.bark("ETH-A", address(this), address(this));
        dog.bark("YFI-A", address(this), address(this));
        dog.bark("LINK-A", address(this), address(this));

        assertEq(clipETHA.kicks(), auctionIdETHA);
        assertEq(clipYFIA.kicks(), auctionIdYFIA);
        assertEq(clipLINKA.kicks(), auctionIdLINKA);

        hevm.store(
            address(end),
            keccak256(abi.encode(address(this), uint256(0))),
            bytes32(uint256(1))
        );
        assertEq(end.wards(address(this)), 1);

        end.cage();
        end.cage("ETH-A");
        end.cage("YFI-A");
        end.cage("LINK-A");

        (,,, address usr,,) = clipETHA.sales(auctionIdETHA);
        assertTrue(usr != address(0));
        (,,, usr,,) = clipYFIA.sales(auctionIdYFIA);
        assertTrue(usr != address(0));
        (,,, usr,,) = clipLINKA.sales(auctionIdLINKA);
        assertTrue(usr != address(0));

        end.snip("ETH-A", auctionIdETHA);
        end.snip("YFI-A", auctionIdYFIA);
        end.snip("LINK-A", auctionIdLINKA);

        (,,, usr,,) = clipETHA.sales(auctionIdETHA);
        assertTrue(usr == address(0));
        (,,, usr,,) = clipYFIA.sales(auctionIdYFIA);
        assertTrue(usr == address(0));
        (,,, usr,,) = clipLINKA.sales(auctionIdLINKA);
        assertTrue(usr == address(0));

        end.skim("ETH-A", address(this));
        end.skim("YFI-A", address(this));
        end.skim("LINK-A", address(this));

        end.free("ETH-A");
        end.free("YFI-A");
        end.free("LINK-A");

        hevm.warp(block.timestamp + end.wait());

        vow.heal(min(vat.dai(address(vow)), sub(sub(vat.sin(address(vow)), vow.Sin()), vow.Ash())));

        // Removing the surplus to allow continuing the execution.
        hevm.store(
            address(vat),
            keccak256(abi.encode(address(vow), uint256(5))),
            bytes32(uint256(0))
        );

        end.thaw();

        end.flow("ETH-A");
        end.flow("YFI-A");
        end.flow("LINK-A");

        vat.hope(address(end));

        uint256 daiToRedeem = vat.dai(address(this)) / RAY;
        assertTrue(daiToRedeem > 0);

        end.pack(daiToRedeem);

        // Make sure amount of collateral in the End doesn't limit process (as otherwise we need to skim vaults)
        hevm.store(
            address(vat),
            keccak256(abi.encode(address(end), keccak256(abi.encode(bytes32("ETH-A"), uint256(4))))),
            bytes32(bytes32(type(uint256).max))
        );
        hevm.store(
            address(vat),
            keccak256(abi.encode(address(end), keccak256(abi.encode(bytes32("YFI-A"), uint256(4))))),
            bytes32(bytes32(type(uint256).max))
        );
        hevm.store(
            address(vat),
            keccak256(abi.encode(address(end), keccak256(abi.encode(bytes32("LINK-A"), uint256(4))))),
            bytes32(bytes32(type(uint256).max))
        );

        end.cash("ETH-A", daiToRedeem);
        end.cash("YFI-A", daiToRedeem);
        end.cash("LINK-A", daiToRedeem);
    }

    function testFlashLegacy() public {
        FlashAbstract flash = FlashAbstract(addr.addr("MCD_FLASH_LEGACY"));
        assertEq(flash.max(), 500_000_000 * WAD);

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        uint256 vowDai = vat.dai(address(vow));

        uint256 amount = 1 * MILLION;
        assertEq(flash.toll(), 0);
        assertEq(flash.flashFee(address(dai), amount * WAD), 0);
        assertEq(flash.max(), 250_000_000 * WAD);
        assertEq(flash.flashFee(address(dai), amount * WAD), 0); // 0 DAI fee on a 1M loan (on any actually)
        flash.flashLoan(address(this), address(dai), amount * WAD, "");
        flash.vatDaiFlashLoan(address(this), amount * RAD, "");
        assertEq(vat.sin(address(flash)), 0);
        assertEq(vat.dai(address(flash)), 0);
        flash.accrue();
        assertEq(vat.dai(address(flash)), 0);
        assertEq(vat.dai(address(vow)), vowDai);
    }

    function testFlash() public {
        FlashAbstract flash = FlashAbstract(addr.addr("MCD_FLASH"));

        vote(address(spell));
        scheduleWaitAndCast(address(spell));
        assertTrue(spell.done());

        uint256 amount = 1 * MILLION;
        assertEq(flash.flashFee(address(dai), amount * WAD), 0);
        assertEq(flash.max(), 250_000_000 * WAD);
        assertEq(flash.flashFee(address(dai), amount * WAD), 0); // 0 DAI fee on a 1M loan (on any actually)
        flash.flashLoan(address(this), address(dai), amount * WAD, "");
        flash.vatDaiFlashLoan(address(this), amount * RAD, "");
        assertEq(vat.sin(address(flash)), 0);
        assertEq(vat.dai(address(flash)), 0);
    }

    function onFlashLoan(
        address initiator,
        address token,
        uint256 amount,
        uint256 fee,
        bytes calldata
    ) external returns (bytes32) {
        assertEq(initiator, address(this));
        assertEq(token, address(dai));
        assertEq(amount, 1 * MILLION * WAD);
        assertEq(fee, 0);

        dai.approve(msg.sender, amount);

        return keccak256("ERC3156FlashBorrower.onFlashLoan");
    }

    function onVatDaiFlashLoan(
        address initiator,
        uint256 amount,
        uint256 fee,
        bytes calldata
    ) external returns (bytes32) {
        assertEq(initiator, address(this));
        assertEq(amount, 1 * MILLION * RAD);
        assertEq(fee, 0);

        vat.move(address(this), msg.sender, amount);

        return keccak256("VatDaiFlashBorrower.onVatDaiFlashLoan");
    }
}

interface EndNewAbstract is EndAbstract {
    function cure() external view returns (address);
}

interface CureAbstract {
    function wards(address) external view returns (uint256);
}
